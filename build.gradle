plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2' apply false
}

group = 'com.sakurarealm.recipecraft'
version = '1.0-SNAPSHOT'

def localDependencies = [
        'GermPlugin-4.2.9',
]

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow'

    repositories {
        mavenCentral()
        maven {
            name = "spigotmc-repo"
            url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/groups/public/"
        }

        flatDir {
            dirs '../libs'
        }
    }

    dependencies {
        compileOnly "org.spigotmc:spigot-api:1.12.2-R0.1-SNAPSHOT"

        localDependencies.forEach {
            compileOnly name: it
        }

        testImplementation platform('org.junit:junit-bom:5.9.1')
        testImplementation 'org.junit.jupiter:junit-jupiter'
    }

    java {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
        withSourcesJar()
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }

    test {
        useJUnitPlatform()
    }

    processResources {
        def props = [version: version]
        inputs.properties props
        filteringCharset 'UTF-8'
        filesMatching('plugin.yml') {
            expand props
        }
    }
}
